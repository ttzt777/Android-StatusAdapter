apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// 定义参数
def repoDef = "util"
def groupDef = "cc.bear3.util.statusadapter"
def artifactDef = "statusadapter"
def versionDef = "1.0.0"
def descriptionDef = "Bass adapter for recycler view!"
def urlDef = "https://github.com/ttzt777/Android-StatusAdapter"

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/lib-release.aar")
            groupId "$groupDef"
            artifactId "$artifactDef"
            version "$versionDef"
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray_user") // local.properties里设置
    key = properties.getProperty("bintray_apikey") // local.properties里设置
    publications = ['Production']
    publish = true
    override = false
    dryRun = false
    pkg {
        repo = "$repoDef"
        name = "$artifactDef"
        description = "$descriptionDef"
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        websiteUrl = "$urlDef"
        vcsUrl = "$urlDef"

        version {
            name = "$versionDef"
            desc = "Version $versionDef"
            released = new Date()
            vcsTag = "$versionDef"
        }
    }
}